;;;  Authors: Michael Littman and David Weissman  ;;;

(define (domain g-tire-world-pre)
	(:requirements :typing :strips :negative-preconditions
			:probabilistic-effects :disjunctive-preconditions
			:conditional-effects)
	(:types location)
	(:predicates	(vehicle-at ?loc - location)
			(hasspare-vehicle)
			(hasspare-location ?loc - location)
			(road ?from - location ?to - location)
			(flattire))
	(:action mov-car
		:parameters (?from - location ?to - location)
		:precondition (and (vehicle-at ?from) (road ?from ?to)
					(not (flattire)))
		:effect (and (vehicle-at ?to) (not (vehicle-at ?from))
				(probabilistic .15 (flattire)))
	)

	(:action loadtire
		:parameters (?loc - location)
		:precondition (and (vehicle-at ?loc) (hasspare-location ?loc) 
					(not (hasspare-vehicle)))
		:effect (and (hasspare-vehicle) (not (hasspare-location ?loc)))
	)

	(:action changetire
		:precondition (and (hasspare-vehicle) (flattire))
		:effect (and (not (hasspare-vehicle))
				 (not (flattire))
			  )
	)

)

(define (problem g-tire-problem-pre)
	(:domain g-tire-world-pre)
	(:objects c0 c1 c2 c3 c4 c5 c6 c7 c8 c9 ca cb cc cd ce cf cg ch ci cj ck cl cm cn 
		    d0 d1 d2 d3 d4 d5 d6 - location)
	(:init
		(vehicle-at c0)
		(road c0 c1)
		(road c1 c2)
		(road c2 c3)
		(road c3 c4)
		(road c4 c5)
		(road c5 c6)
		(road c6 c7)
		(road c7 c8)
		(road c8 c9)
		(road c0 ca)
		(road ca cb)
		(road cb cc)
		(road cc cd)
		(road cb ce)
		(road cd ce)
		(road ce cf)
		(road cf d5)
		(road d5 d6)
		(road d6 cg)
		(road cf cg)
		(road cg ch)
		(road ch ci)
		(road ci cj)
		(road cj ck)
		(road ck cl)
		(road cl c9)
		(road ch cm)
		(road cm cn)
		(road cn ci)
		(road c6 d0)
		(road d0 d1)
		(road d1 d2)
		(road d2 d3)
		(road d3 d4)
		(road d4 c8)
		(hasspare-location cc)
		(hasspare-location d5)
		(hasspare-location ck)
		(hasspare-location cm)
		(hasspare-location d4)
	)
	(:goal (vehicle-at c9))
)

